
# This script follows 
# http://www.gnu.org/software/autoconf/manual/html_node/Autoconf-Input-Layout.html#Autoconf-Input-Layout
# Please keep it like that.

# Guessing some version.
AC_PREREQ(2.54)

m4_define([ccss_version_major], [0])
m4_define([ccss_version_minor], [3])
m4_define([ccss_version_micro], [0])
### For the release:
# m4_define([ccss_version_extra], [])
### On trunk:
m4_define([ccss_version_extra], [.trunk])
m4_define([ccss_version],
    [ccss_version_major.ccss_version_minor.ccss_version_micro[]ccss_version_extra])

AC_INIT([gtk-css-engine], [ccss_version])
AC_CONFIG_HEADERS([config.h])
AC_CONFIG_HEADERS([ccss/ccss-features.h])
AC_CONFIG_SRCDIR([ccss/ccss-stylesheet.c])

AM_INIT_AUTOMAKE
AM_MAINTAINER_MODE

dnl Version info for libraries = CURRENT:REVISION:AGE
dnl
dnl Within each x.y.*, ABI is maintained backward and _forward_ compatible.
dnl (As a consequence, no exported function may be added.)
dnl So it's enough to have one interface number per each x.y.* branch.
dnl
dnl OTOH, we are not able to keep ABI strictly backward compatible throughout
dnl the whole x.*.*.
dnl The easiest way is to declare no ABI compatibility, ie. AGE is always 0.
dnl
m4_define([version_iface],
	m4_eval(100 * ccss_version_major + ccss_version_minor))

AC_SUBST([VERSION_INFO], [version_iface:ccss_version_micro:0])


### Checks for configure arguments. ############################################

AC_ARG_ENABLE([debug], 
  [AS_HELP_STRING([--enable-debug], [enable debug code])], 
[
  enable_debug="$enableval"
],[
  enable_debug="no"
])

if test "$enable_debug" == "yes"; then
  AC_DEFINE([CCSS_DEBUG], [1], [enable debug code])
  CFLAGS="-g -O0 ${CFLAGS}"
fi
AM_CONDITIONAL([CCSS_DEBUG], test "$enable_debug" == "yes")


# So usually one just sets CFLAGS for this, but a configure 
# arguments lets us force this when running `make distcheck'.
AC_ARG_ENABLE([werror], 
  [AS_HELP_STRING([--enable-werror], [bail on warnings])], 
[
  if test "$enableval" == "yes"; then
    CFLAGS="$CFLAGS -Werror"
  fi  
])


### Checks for programs. #######################################################

AC_PROG_CC
AM_PROG_CC_C_O
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

# Should be replaced by LT_INIT, which apparently doesn't work.
# Try again some time.
AC_DISABLE_STATIC
AC_LIBTOOL_WIN32_DLL
AC_PROG_LIBTOOL 

# gtk-doc
gtk_doc_installed=true
#ifdef([GTK_DOC_CHECK],[GTK_DOC_CHECK([1.0])],[gtk_doc_installed=false])
# I would have liked to conditionalize this, but 
# that appears to break other things http://bugzilla.gnome.org/show_bug.cgi?id=156399
GTK_DOC_CHECK([1.0])
AM_CONDITIONAL(GTK_DOC_INSTALLED, $gtk_doc_installed)


### Checks for libraries. ######################################################

# Maybe it would work with gtk+-2.8, the first sporting cairo, right?
pkgs='gtk+-2.0 >= 2.10.0 libcroco-0.6'


# See http://bugzilla.gnome.org/show_bug.cgi?id=553937 .
PKG_CHECK_EXISTS([ libcroco-0.6 >= 0.6.2 ],
[
  enable_libcroco_workaround="no"
],[
  enable_libcroco_workaround="yes"
])
if test "$enable_libcroco_workaround" = "yes"; then
  AC_DEFINE([CCSS_ENABLE_LIBCROCO_WORKAROUND], [1], [Work around libcroco issue #553937])
fi


# Guessing a version.
rsvg_req='librsvg-2.0 >= 2.16'
PKG_CHECK_EXISTS([ $rsvg_req ],
[
  with_rsvg="yes"
  pkgs="$pkgs $rsvg_req"
],[
  with_rsvg="no"
])

if test "$with_rsvg" = "yes"; then
  AC_DEFINE([CCSS_WITH_RSVG], [1], [SVG support through librsvg])
fi
AM_CONDITIONAL([CCSS_WITH_RSVG], [test "$with_rsvg" = "yes"])


soup_req='libsoup-2.4'
PKG_CHECK_EXISTS([ $soup_req ],
[
  with_soup="yes"
  pkgs="$pkgs $soup_req"
],[
  with_soup="no"
])

if test "$with_soup" = "yes"; then
  AC_DEFINE([CCSS_WITH_SOUP], [1], [URI parsing support through libsoup])
fi
AM_CONDITIONAL([CCSS_WITH_SOUP], [test "$with_soup" = "yes"])


clutter_req='clutter-cairo-0.8'
PKG_CHECK_EXISTS([ $clutter_req ],
[
  with_clutter="yes"
  pkgs="$pkgs $clutter_req"
],[
  with_clutter="no"
])

AM_CONDITIONAL([CCSS_WITH_CLUTTER], [test "$with_clutter" = "yes"])


PKG_CHECK_MODULES(CCSS, $pkgs)

AC_SUBST([CCSS_CFLAGS])
AC_SUBST([CCSS_LIBS])
AC_SUBST([CCSS_DEPS], $pkgs)

AC_DEFINE([CCSS_WITH_GTK], [1], [Support for drawing Gtk+ primitives like `box-gap'])
AM_CONDITIONAL([CCSS_WITH_GTK], [test "yes" = "yes"])

GTK_VERSION=`$PKG_CONFIG --variable=gtk_binary_version gtk+-2.0`
AC_SUBST(GTK_VERSION)


### Checks for header files. ###################################################

AC_HEADER_STDC
AC_CHECK_HEADERS([stdbool.h stdint.h stdio.h stdlib.h string.h])


### Checks for typedefs, structures, and compiler characteristics. #############

AC_C_CONST

if test "$GCC" = "yes"; then
  for option in -std=c99 -pedantic -Wall -Wbitwise -Wcast-to-as			\
    -Wchar-subscripts -Wdeclaration-after-statement -Wdefault-bitfield-sign	\
    -Wdo-while -Wmissing-declarations -Wmissing-noreturn -Wmissing-prototypes	\
    -Wnested-externs -Wno-pointer-sign -Wparen-string -Wpointer-arith		\
    -Wptr-subtraction-blows -Wreturn-void -Wsign-compare -Wstrict-prototypes	\
    -Wtypesign -Wwrite-strings ; do
    SAVE_CFLAGS="$CFLAGS"
    CFLAGS="$CFLAGS $option"
    AC_MSG_CHECKING([whether gcc understands $option])
    AC_COMPILE_IFELSE([AC_LANG_PROGRAM([[]], [[]])],[has_option=yes],[has_option=no])
    if test $has_option = no; then
      CFLAGS="$SAVE_CFLAGS"
    fi
    AC_MSG_RESULT($has_option)
    unset has_option
    unset SAVE_CFLAGS
  done
  unset option
fi

AC_CONFIG_FILES([
  Makefile
  libccss-1.pc
  cccss/Makefile
  ccss/Makefile
  doc/Makefile
  doc/version.xml
  examples/Makefile
  src/Makefile
])
AC_OUTPUT

echo "
Configuration
-------------

Libccss:
    CFLAGS                       ${CFLAGS}
    Build debugging code         $enable_debug
    Support for SVG images       $with_rsvg (requires librsvg)
WIP Support for SVG fragments    $with_soup (requires libsoup)
WIP Clutter support              $with_clutter
"

